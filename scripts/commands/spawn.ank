bind = import("bind")
log = import("log")
world = import("world")
regexp = import("regexp")
strings = import("strings")

bind.command("npc", func(player, args) {
	if len(args) < 1 {
		player.Message("Invalid syntax.  Usage: ::npc <id> (<radius>)")
		return
	}
	try {
		x = player.X()
		y = player.Y()
		id = toInt(args[0])
		if id < 0 || id >= len(npcDefs) {
			return
		}
		rad = 5
		if len(args) > 1 {
			rad = toInt(args[1])
		}
		if rad < 0 {
			rad = 0
		}

		if len(args) > 2 {
			for i = 0; i < toInt(args[2]); i++ {
				world.addNpc(newNpc(id, x, y, x-rad, x+rad, y-rad, y+rad))
			}
		} else {
			world.addNpc(newNpc(id, x, y, x-rad, x+rad, y-rad, y+rad))
		}
		log.cmdf("'%v' spawned a new NPC{id:%v, rad:%v} at %v,%v", player.String(), id, rad, x, y)
	} catch {
		player.Message("Invalid syntax.  Usage: ::npc <id> (<radius>)")
		return
	}
})

bind.command("item", func(player, args) {
	id = -1
	amount = 1
	if len(args) > 1 {
		amount = toInt(args[1])
	}
	if regexp.MustCompile("^[0-9]+$").MatchString(args[0]) {
		id = toInt(args[0])
	} else {
		ids = fuzzyItem(args[0])
		if len(ids) < 1 {
			player.Message("Could not fuzzysearch a match for " + args[0])
			return
		}
		if len(ids) >= 27 {
			player.Message("Search name too ambiguous: Too many results matched(" + len(ids) + ")")
			return
		}

		if len(ids) > 1 {
			names = []
			for match in ids {
				names += match.name
			}
			player.Message("Choose which item you wish to spawn")
			reply = player.OpenOptionMenu(names...)
			if reply == -1 {
				player.Message("::item menu timeout")
				return
			}


			id = toInt(ids[toInt(reply)].id)
		} else {
			id = toInt(ids[0].id)
			if id < 0 || id > 1289 {
				player.Message("Item ID out of bounds.  Must be between 0 and 1289, or use its name eg 'santas hat'")
				return
			}
		}
    }
	if id >= 0 && id <= 1289 {
		player.AddItem(id, amount)
	}
})

bind.command("object", func(player, args) {
	x = player.X()
	y = player.Y()
	if world.getObjectAt(x, y) != nil {
		player.Message("There is already an object here.")
		return
	}
	try {
		id = toInt(args[0])
		if id < 0 || id >= len(objectDefs) {
			return
		}
		dir = NORTH
		if len(args) > 1 {
			dir = parseDirection(args[1])
		}
		world.addObject(newObject(id, NORTH, x, y, false))
		log.cmdf("'%v' spawned a new object{id:%v, dir:%v} at %v,%v", player.String(), id, dir, x, y)
	} catch e {
		player.Message("Invalid syntax.  Usage: ::object <id> (<dir>)")
	}
})

bind.command("dobj", func(player, args) {
	if len(args) == 0 {
		args = [toString(player.X()), toString(player.Y())]
	}
	if len(args) < 2 {
		player.Message("Invalid args.  Usage: ::dobj <x> <y>")
		return
	}
	x = toInt(args[0])
	y = toInt(args[1])
	object = world.getObjectAt(x, y)
	if object == nil {
		player.Message("Can not find object.")
		return
	}

	log.cmdf("%v deleted object %v", player, object)
	world.removeObject(object)
})

bind.command("boundary", func(player, args) {
	x = player.X()
	y = player.Y()
	if world.getObjectAt(x, y) != nil {
		player.Message("There is already an object here.")
		return
	}
	try {
		id = toInt(args[0])
		if id < 0 || id >= len(boundaryDefs) {
			return
		}
		dir = NORTH
		if len(args) > 1 {
			dir = parseDirection(args[1])
		}
		world.addObject(newObject(id, NORTH, x, y, true))
		log.cmdf("'%v' spawned a new boundary{id:%v, dir:%v} at %v,%v", player.String(), id, dir, x, y)
	} catch e {
		player.Message("Invalid syntax.  Usage: ::boundary <id> (<dir>)")
	}
})
