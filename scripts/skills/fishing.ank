bind = import("bind")
strings = import("strings")
world = import("world")

// Contains definitions for what objects contain what fish, etc
load("scripts/def/fishing.ank")

bind.object(objectPredicate(keys(defs)...), func(player, object, click) {
	cmd = strings.ToLower(objectDefs[object.ID].Commands[click])
	fishDef = defs[toInt(object.ID)]
	fish = getFish(fishDef[click], player.Skills().Current(FISHING), click)

	fishName = strings.Replace(strings.ToLower(itemDefs[fish.id].Name), "raw ", "", -1)
	if player.Inventory.CountID(fishDef[click].net) < 1 {
		player.Message("You need a " + itemDefs[fishDef[click].net].Name + " to " + (cmd != "cage" ? cmd : "catch") + " " + (cmd != "cage" ? "these fish" : (fishName + "s")))
		return
	}
	if fish.lvl > player.Skills().Current(FISHING) {
		player.Message("You need at least level " + toString(fish.lvl) + " to " + (cmd != "cage" ? cmd : "catch") + " " + (cmd != "cage" ? "these fish" : (fishName + "s")))
		return
	}
	if fishDef[click].bait >= 0 {
		if player.Inventory.CountID(fishDef[click].bait) <= 0 {
			player.Message("You don't have any " + strings.ToLower(itemDefs[fishDef[click].bait].Name) + " left")
			return
		}
	}
	player.PlaySound("fishing")
	player.ItemBubble(fishDef[click].net)
	player.Message("You attempt to catch " + (fishDef[click].net == ids.NET ? "some" : "a") + " " + (fishDef[click].net == ids.LOBSTER_POT ? "lobster" : "fish"))
	sleep(Millisecond*1920)


	if gatheringSuccess(fish.lvl, player.Skills().Current(FISHING)) && world.getObjectAt(object.X(), object.Y()) == object {
		if fishDef[click].bait >= 0 {
			player.Inventory.RemoveByID(fishDef[click].bait, 1)
		}
		player.Message("You catch " + (fishDef[click].net == ids.NET ? "some" : "a") + " " + (fishDef[click].net == ids.LOBSTER_POT ? "lobster" : "fish"))
		player.AddItem(fish.id, 1)
		player.IncExp(FISHING, fish.exp)
		return
	}
	player.Message("You fail to catch anything")
})
