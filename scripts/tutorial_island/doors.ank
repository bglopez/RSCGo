bind = import("bind")
world = import("world")

lockedDoors = [
	{
		"name": "guide",
		"loc": [222, 743],
		"stage": 10,
	},
	{
		"name": "controls guide",
		"loc": [224, 737],
		"stage": 15,
	},
	{
		"name": "combat instructor",
		"loc": [220, 727],
		"stage": 25,
	},
	{
		"name": "cooking instructor",
		"loc": [212, 729],
		"stage": 35,
	},
	{
		"name": "finance advisor",
		"loc": [206, 730],
		"stage": 40,
	},
	{
		"name": "fishing instructor",
		"loc": [201, 734],
		"stage": 45,
	},
	{
		"name": "mining instructor",
		"loc": [198, 746],
		"stage": 55,
	},
	{
		"name": "bank assistant",
		"loc": [204, 752],
		"stage": 60,
	},
	{
		"name": "quest advisor",
		"loc": [209, 754],
		"stage": 65,
	},
	{
		"name": "wilderness guide",
		"loc": [217, 760],
		"stage": 70,
	},
	{
		"name": "magic instructor",
		"loc": [222, 760],
		"stage": 80,
	},
	{
		"name": "fatigue expert",
		"loc": [226, 760],
		"stage": 90,
	},
	{
		"name": "community instructor",
		"loc": [230, 759],
		"stage": 100,
	},
]

bind.boundary(objectPredicate(83, 77, 90, 85, 88, 84, 82, 75, 81, 80, 89, 78, 76), func(player, object, click) {
	isLocked = func() {
		for locked in lockedDoors {
			if object.X() == locked.loc[0] && object.Y() == locked.loc[1] {
				if toInt(player.Cache("tutorial")) < locked.stage {
					player.Message("You should speak to the " + locked.name + " before going through this door")
					return true
				}
				break
			}
		}
		return false
	}

    if isLocked() {
        return
    }
	player.PlaySound("opendoor")
	oldID = object.ID
	destX = object.X()
	destY = object.Y()
	if object.Direction == 0 {
		if player.X() == destX && player.Y() == destY {
			destY -= 1
		}
	} else if object.Direction == 1 {
		if player.X() == destX && player.Y() == destY {
			destX -= 1
		}
	} else if object.Direction == 2 {
		if (object.X() == player.X()) && (object.Y() == player.Y() + 1) {
			destY += 1
		} else if (object.X() == player.X() - 1) && (object.Y() == player.Y()) {
			destX -= 1
		} else if (object.X() == player.X()) && (object.Y() == player.Y() - 1) {
			destY -= 1
		} else if (object.X() == player.X() + 1) && (object.Y() == player.Y()) {
			destX += 1
		}
	} else if object.Direction == 3 {
		if (object.X() == player.X()) && (object.Y() == player.Y() - 1) {
			destY -= 1
		} else if (object.X() == player.X() + 1) && (object.Y() == player.Y()) {
			destX += 1
		} else if (object.X() == player.X()) && (object.Y() == player.Y() + 1) {
			destY += 1
		} else if (object.X() == player.X() - 1) && (object.Y() == player.Y()) {
			destX -= 1
		}
	}
	openDoor = world.replaceObject(object, 11)
	player.Teleport(destX, destY)
	go runAfter(3*Second, func() {
		player.PlaySound("closedoor")
		world.replaceObject(openDoor, oldID)
	})
})
